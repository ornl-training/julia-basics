{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "3677201b-30cc-419e-be16-fe38fcebb6f9",
   "metadata": {},
   "source": [
    "# Functions and Methods and Multiple Dispatch\n",
    "Manual: [Functions](https://docs.julialang.org/en/v1/manual/functions/), [Methods](https://docs.julialang.org/en/v1/manual/methods)\n",
    "\n",
    "Functions are the primary building blocks in Julia. Every operation is done through functions (even math operations, like `+`)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "884b1a3d-eb8e-4e0a-adef-d4073d4f51dd",
   "metadata": {},
   "outputs": [],
   "source": [
    "@edit 2+2 # (output is ugly in a notebook)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "24f655dc-0826-4d3a-bfe3-37d049ad8e38",
   "metadata": {},
   "source": [
    "Functions return the result of the last evaluation, and this enables the short-hand notation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "57d239f9-79d5-42aa-be3b-483906c8ec59",
   "metadata": {},
   "outputs": [],
   "source": [
    "# full form\n",
    "function f(x)\n",
    "    @show typeof(x)\n",
    "    return x^2 # the keyword `return` can be omitted, but it's generally good to include for readability\n",
    "end\n",
    "\n",
    "# short form\n",
    "g(x) = 2*x\n",
    "\n",
    "@show g(2)\n",
    "\n",
    "@show typeof(f(2))\n",
    "@show typeof(f(2.0))\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0919ac55-f0f8-464b-b29b-2e561013497b",
   "metadata": {},
   "source": [
    "The parameter `x` above being untyped is unconstrained and takes `Any`. We get a different version from the compiled for each type passed in."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b527267f-cfb2-4c8e-aea8-0e47e0b46302",
   "metadata": {},
   "outputs": [],
   "source": [
    "@code_llvm g(2)\n",
    "@code_llvm g(2.0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "743f2455-ee8a-4baa-b659-efcc6b05b174",
   "metadata": {},
   "outputs": [],
   "source": [
    "@code_native g(2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7b1f49ad-f58d-41be-884a-bebd6ebcbdbc",
   "metadata": {},
   "source": [
    "We can constrain the type with an abstract type farther down the tree or with a concrete type and Julia will match with the closest match."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f9ddeebc-6035-4d44-952a-3a2227321b9a",
   "metadata": {},
   "outputs": [],
   "source": [
    "g(x::Integer) = x + x\n",
    "g(x::AbstractFloat) = x * x\n",
    "g(x::Float32) = x\n",
    "@show g(3)\n",
    "@show g(3.0)\n",
    "@show g(3.0f0)\n",
    "\n",
    "h(x::Float32) = x\n",
    "@show h(2)\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bc1bdd27-4a84-430c-afb0-9a3fb966b123",
   "metadata": {},
   "source": [
    "This allows for a lot of flexibility in defining interfaces."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "24de265b-50c8-482e-b61b-153d74d3f48c",
   "metadata": {},
   "source": [
    "### Optional and Keyword Arguments"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "adda09d7-38d4-4e4c-bb3d-85a834a8d69e",
   "metadata": {},
   "outputs": [],
   "source": [
    "# notice the semicolon\n",
    "function keyword_optional(a, b, c=0; d=0, e) # put the optional one at the end in real life :)\n",
    "    @show a, b, c, d, e\n",
    "end\n",
    "\n",
    "keyword_optional(1, 2, 3; d = 4, e = 5)\n",
    "\n",
    "# keyword_optional(1, 2) # error: `e` not assigned\n",
    "\n",
    "keyword_optional(1, 2, e=5) # semicolon at call is not required\n",
    "\n",
    "# keyword_optional(1, 2, 3, 4, 5) # error because you didn't name `d` or `e`"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9f01e1c0-8668-485c-b988-9d295451e885",
   "metadata": {},
   "source": [
    "### `Nothing` Functions\n",
    "Similar to `void` (but with more usefulness), you can `return nothing` if you don't want a value returned from a function."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "27c03045-be15-4106-97a4-8e238f0c0ece",
   "metadata": {},
   "outputs": [],
   "source": [
    "function void1()\n",
    "    return nothing\n",
    "end\n",
    "@show void1()\n",
    "@show typeof(void1())\n",
    "\n",
    "void2() = nothing\n",
    "@show void2()\n",
    "@show typeof(void2())\n",
    "\n",
    "function void3()\n",
    "    return\n",
    "end\n",
    "@show void3()\n",
    "@show typeof(void3())\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "422c3530-9678-4531-ae12-14ad42081164",
   "metadata": {},
   "source": [
    "### Anonymous Functions\n",
    "Similar to lambdas in Python or C++:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "08b90969-06d2-4c03-87dd-71cec2156d74",
   "metadata": {},
   "outputs": [],
   "source": [
    "# assigned to a variable\n",
    "plusThree = (x, y) -> x + y + 3\n",
    "@show plusThree(1,1)\n",
    "sayHello = () -> println(\"Hello!\")\n",
    "@show sayHello()\n",
    "\n",
    "# used in a function\n",
    "a = [1, 2, 3]\n",
    "b = map(x -> x^2, a) # for single argument, you can omit the ()\n",
    "@show b\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a94f2f31-3ec1-44c3-b9a0-fb2a1ac1add6",
   "metadata": {},
   "source": [
    "### Constructors and Functors\n",
    "\n",
    "Manual: [Constructors](https://docs.julialang.org/en/v1/manual/constructors/),\n",
    "[Functors](https://docs.julialang.org/en/v1/manual/methods/#Function-like-objects)\n",
    "\n",
    "Julia types do not have member functions. The two (or 2.5) kinds of functions associated with types are constructors and functors."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ecd8b976-7f60-4a48-9885-2b7efc895b89",
   "metadata": {},
   "outputs": [],
   "source": [
    "struct MyData\n",
    "    x::Float64\n",
    "    x2::Float64\n",
    "    y::Float64\n",
    "    z::Float64\n",
    "\n",
    "    # inner constructor method (if you want to check for errors)\n",
    "    function MyData(x::Float64, x2::Float64, y::Float64, z::Float64)\n",
    "        if x < 0.0\n",
    "            error(\"Can't use negative x value\")\n",
    "        end\n",
    "        new(x,x2,y,z) # `new` available only to inner constructor methods\n",
    "    end\n",
    "end\n",
    "\n",
    "# outer constructor method (useful for custom or simplified constructors)\n",
    "function MyData(x::Float64, y::Float64)\n",
    "    x2=x^2\n",
    "    z = sin(x2+y)\n",
    "    MyData(x, x2, y, z) # default constructor\n",
    "end\n",
    "    \n",
    "\n",
    "println(\"Custom constructor: \", MyData(2.0, 3.0) )\n",
    "println(\"Default constructor: \", MyData(2.0, 4.0, 3.0, 0.6569865987187891) )"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a49c76fe-bad6-476b-8d99-4cc24d0cf550",
   "metadata": {},
   "outputs": [],
   "source": [
    "struct PrintFunc\n",
    "    a::String\n",
    "end\n",
    "\n",
    "# Use the type instead of a function name\n",
    "function (func::PrintFunc)(arg)\n",
    "    println(func.a, \" \", arg)\n",
    "end\n",
    "\n",
    "func = PrintFunc(\"hello\")\n",
    "# Then you can call an object as a function\n",
    "func(2)\n",
    "func(\"Philip\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1c6c799f-5f72-4a2a-ae6d-ef95d595c0c2",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.6",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
