{
  "cells": [
    {
      "cell_type": "markdown",
      "id": "26492217-61b9-46ac-95ae-72875f8a2677",
      "metadata": {
        "id": "26492217-61b9-46ac-95ae-72875f8a2677"
      },
      "source": [
        "# Arrays\n",
        "Multi-dimensional (heap-allocated) arrays are standard library data structures in Julia, with a rich set of tools for manipulation, reshaping, slices, views.\n",
        "\n",
        "Manual: [Arrays](https://docs.julialang.org/en/v1/manual/arrays/)"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "b1926057-7497-4e98-8896-db04151286fc",
      "metadata": {
        "id": "b1926057-7497-4e98-8896-db04151286fc"
      },
      "outputs": [],
      "source": [
        "a = [1 2 3 1; 4 5 6 4; 7 8 9 7]\n",
        "print(\"a: \")\n",
        "display(a)\n",
        ";"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "390fc92c-e1a6-4f84-ba4e-af5b52a37dde",
      "metadata": {
        "id": "390fc92c-e1a6-4f84-ba4e-af5b52a37dde"
      },
      "source": [
        "## Array Indexing\n",
        "Arrays in Julia:\n",
        "- stored __*column-major*__\n",
        "- indexed starting at 1."
      ]
    },
    {
      "cell_type": "code",
      "source": [
        "@show a[1,1]\n",
        "@show a[3,2]\n",
        ";"
      ],
      "metadata": {
        "id": "-xyCJJICPPBG"
      },
      "id": "-xyCJJICPPBG",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "5f41f609-d938-4c4b-ad4d-aeaf2a9af21a",
      "metadata": {
        "id": "5f41f609-d938-4c4b-ad4d-aeaf2a9af21a"
      },
      "outputs": [],
      "source": [
        "print(\"a_flat:\")\n",
        "a_flat = reshape(a, length(a))\n",
        "display(a_flat)\n",
        ";"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "4cf33122-d013-40eb-97cd-169ec22bef99",
      "metadata": {
        "id": "4cf33122-d013-40eb-97cd-169ec22bef99"
      },
      "outputs": [],
      "source": [
        "a_flat[1] = -1\n",
        "display(a_flat)\n",
        "display(a)\n",
        ";"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "0ace2ce3-2b7d-411f-a94a-456e0995d9c5",
      "metadata": {
        "id": "0ace2ce3-2b7d-411f-a94a-456e0995d9c5"
      },
      "outputs": [],
      "source": [
        "# NOTE: This is just to demonstrate the array ordering;\n",
        "# There are very few contexts in Julia in which raw pointers are needed\n",
        "println(\"@a[1,2]:    \", pointer(view(a, 1, 2)))\n",
        "println(\"@a_flat[4]: \", pointer(a_flat, 4))\n",
        "@show unsafe_load(pointer(a_flat, 4))\n",
        ";"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "c2c64703-3b4a-4353-b8c9-579ecb8d84a8",
      "metadata": {
        "id": "c2c64703-3b4a-4353-b8c9-579ecb8d84a8"
      },
      "outputs": [],
      "source": [
        "a_T = transpose(a)\n",
        "display(a_T)\n",
        "println(\"@a_T[2,1]:  \", pointer(view(a_T, 2, 1)))\n",
        ";"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "f1fcbfcc-1f34-40a1-a21a-252aba4d7b66",
      "metadata": {
        "id": "f1fcbfcc-1f34-40a1-a21a-252aba4d7b66"
      },
      "source": [
        "So, when nesting loops, remember that the first index should be the fastest."
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "71aeb486-a5ed-4c18-a230-37ff24c46f0a",
      "metadata": {
        "id": "71aeb486-a5ed-4c18-a230-37ff24c46f0a"
      },
      "outputs": [],
      "source": [
        "for j in 1:3\n",
        "    for i in 1:3\n",
        "        println(i, \", \", j, \": \", a[i,j])\n",
        "    end\n",
        "end"
      ]
    },
    {
      "cell_type": "markdown",
      "source": [
        "__*WARNING*__: reshape and transpose are adapters but *slicing makes a copy!*\n",
        "\n",
        "Use views to avoid copying."
      ],
      "metadata": {
        "id": "_ebKeTmXSFFF"
      },
      "id": "_ebKeTmXSFFF"
    },
    {
      "cell_type": "code",
      "source": [
        "a_slice = a[1, :]\n",
        "a_slice[2] = 100\n",
        "@show typeof(a_slice)\n",
        "display(a)\n",
        "\n",
        "a_view = @view a[1, :]\n",
        "a_view[2] = 100\n",
        "@show typeof(a_view)\n",
        "display(a)"
      ],
      "metadata": {
        "id": "zcP8BuXzSBAm"
      },
      "id": "zcP8BuXzSBAm",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "id": "30ed4ff8-7475-478a-9f12-afc7a663769d",
      "metadata": {
        "id": "30ed4ff8-7475-478a-9f12-afc7a663769d"
      },
      "source": [
        "## Array Broadcasting\n",
        "Unary and binary functions on individual elements can be broadcast to all elements of an array (or two arrays). Nested broadcast operations are fused into one. The `@.` macro can be used to broadcast all operations in an expression without using the `.` everywhere."
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "de0334ec-516a-4496-9d4b-c2f48605011e",
      "metadata": {
        "id": "de0334ec-516a-4496-9d4b-c2f48605011e"
      },
      "outputs": [],
      "source": [
        "function bc()\n",
        "    N = 20_000_000\n",
        "    u = randn(N)\n",
        "    v = randn(N)\n",
        "    w = randn(N)\n",
        "\n",
        "    println()\n",
        "\n",
        "    # free fusion\n",
        "    @time begin\n",
        "        tmp = u + v\n",
        "        fin = tmp + w\n",
        "    end\n",
        "    @time u + v + w\n",
        "\n",
        "    println()\n",
        "\n",
        "    @time begin\n",
        "        # typical non-optimized result\n",
        "        tmp1 = sin.(u)\n",
        "        tmp2 = cos.(v)\n",
        "        tmp3 = tmp1 .* tmp2\n",
        "        tmp4 = cos.(w)\n",
        "        tmp5 = sin.(tmp4)\n",
        "        fin1 = tmp3 .* tmp5\n",
        "    end\n",
        "    @time begin\n",
        "        fin2 = sin.(u) .* cos.(v) .* sin.(cos.(w))\n",
        "    end\n",
        "    @time begin\n",
        "        # This is basically what the above expression is expanded to\n",
        "        fin3 = broadcast((x,y,z) -> sin(x) * cos(y) * sin(cos(z)), u, v, w)\n",
        "    end\n",
        "    @time begin\n",
        "        # Pre-allocate in order to use .=\n",
        "        fin4 = similar(u)\n",
        "        # Equivalent to\n",
        "        # fin4 .= sin.(u) .* cos.(v) .* sin.(cos.(w))\n",
        "        @. fin4 = sin(u) * cos(v) * sin(cos(w))\n",
        "    end\n",
        "\n",
        "    @show (fin1 == fin2 == fin3 == fin4)\n",
        "end\n",
        "bc()\n",
        "bc()\n",
        "bc()\n",
        ";"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "bde27ed1-eee3-4702-b981-8b07ac5d3c1e",
      "metadata": {
        "id": "bde27ed1-eee3-4702-b981-8b07ac5d3c1e"
      },
      "source": [
        "This is handy for non-numerical applications as well..."
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "51a66046-64c4-452a-94a6-e029ea626e91",
      "metadata": {
        "id": "51a66046-64c4-452a-94a6-e029ea626e91"
      },
      "outputs": [],
      "source": [
        "names = [\"Brigid\", \"Thecla\", \"Hildegard\"]\n",
        "names .= \"Saint \" .* names .* \", pray for us.\"\n",
        "display(names)\n",
        ";"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "c49ad62d-c148-4be8-b8cb-96ce7038779b",
      "metadata": {
        "id": "c49ad62d-c148-4be8-b8cb-96ce7038779b"
      },
      "outputs": [],
      "source": []
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Julia",
      "name": "julia"
    },
    "language_info": {
      "name": "julia"
    },
    "colab": {
      "provenance": []
    }
  },
  "nbformat": 4,
  "nbformat_minor": 5
}
